.class public Sum
.super java/lang/Object

.method public <init>()V
    aload 0
    invokenonvirtual java/lang/Object/<init>()V
    return
.end method

.method public static main([Ljava/lang/String;)V
    .limit locals 1
    .limit stack  3
    getstatic     java/lang/System/out Ljava/io/PrintStream;
    ldc           6
    dup
    imul
    invokestatic  Sum/sum(I)I
    invokevirtual java/io/PrintStream/println(I)V
    return
.end method

.method public static sum(I)I
    .limit locals 2
    .limit stack  2
    ldc    0
    istore 1            ; s = 0;
loop:
    iload  0            ; load n
    ifeq   finish       ; if (n == 0) goto finish;
    iload  1            ; load s
    iload  0            ; load n
    iadd
    istore  1           ; s = s + n;
    iload   0           ; load n
    ldc     1
    isub
    istore  0           ; n = n - 1;
    goto    loop
finish:
    iload   1
    ireturn             ; return s;
.end method
